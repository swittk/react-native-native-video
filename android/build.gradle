import java.nio.file.Paths

static def findNodeModules(baseDir) {
  def basePath = baseDir.toPath().normalize()
  // Node's module resolution algorithm searches up to the root directory,
  // after which the base path will be null
  while (basePath) {
    def nodeModulesPath = Paths.get(basePath.toString(), "node_modules")
    def reactNativePath = Paths.get(nodeModulesPath.toString(), "react-native")
    if (nodeModulesPath.toFile().exists() && reactNativePath.toFile().exists()) {
      return nodeModulesPath.toString()
    }
    basePath = basePath.getParent()
  }
  throw new GradleException("ReactNativeNativeVideo: Failed to find node_modules/ path!")
}

def nodeModules = findNodeModules(projectDir)
logger.warn("ReactNativeNativeVideo: node_modules/ found at: ${nodeModules}")

def reactProperties = new Properties()
file("$nodeModules/react-native/ReactAndroid/gradle.properties").withInputStream { reactProperties.load(it) }
def REACT_NATIVE_VERSION = reactProperties.getProperty("VERSION_NAME").split("\\.")[1].toInteger()


buildscript {
    if (project == rootProject) {
        repositories {
            google()
            mavenCentral()
            jcenter()
        }

        dependencies {
          classpath 'com.android.tools.build:gradle:4.2.0'
        }
    }
}

apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}
def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['VisionCamera_' + name]
}
def getExtOrDefaultValue(name, defaultValue) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : defaultValue
}


android {
    compileSdkVersion safeExtGet('NativeVideo_compileSdkVersion', 29)
    ndkVersion getExtOrDefaultValue('ndkVersion', '21.4.7075529')
    defaultConfig {
        minSdkVersion safeExtGet('NativeVideo_minSdkVersion', 16)
        targetSdkVersion safeExtGet('NativeVideo_targetSdkVersion', 29)
        versionCode 1
        versionName "1.0"

        externalNativeBuild {
            cmake {
                cppFlags "-O2 -frtti -fexceptions -Wall -fstack-protector-all"
                abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
                // This is how the NODE_MODULES_DIR magic variable appears in our CMAKELISTS file
                arguments "-DNODE_MODULES_DIR=${nodeModules.toString()}",
                  // This is how the REACT_NATIVE_VERSION magic variable appears in our CMAKELISTS file
                  "-DREACT_NATIVE_VERSION=${REACT_NATIVE_VERSION}"
            }
        }

    }

    externalNativeBuild {
      cmake {
        path "CMakeLists.txt"
      }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    lintOptions {
        disable 'GradleCompatible'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenLocal()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url("$nodeModules/react-native/android")
    }
    google()
    mavenCentral()
    jcenter()
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-native:+"  // From node_modules
}
